apply plugin: 'com.google.protobuf'

// TODO: Remove when upgrading to versions of these packages which publish an osx-aarch build
def missingAarchBuildClassifier = osdetector.os == "osx" ? ":osx-x86_64" : ""

dependencies {
    api group: 'io.grpc', name: 'grpc-netty', version: "${grpc_version}"
    api group: 'io.grpc', name: 'grpc-protobuf', version: "${grpc_version}"
    api group: 'io.grpc', name: 'grpc-stub', version: "${grpc_version}"
    api group: 'io.grpc', name: 'grpc-services', version: "${grpc_version}"

    api group: 'com.google.protobuf', name: 'protobuf-java-util', version: "${protobuf_version}"

    implementation 'javax.annotation:javax.annotation-api:1.3.1'
}

java {
    withSourcesJar()
    withJavadocJar()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobuf_version}" + missingAarchBuildClassifier
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}" + missingAarchBuildClassifier
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}
